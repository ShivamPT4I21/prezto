##################################################
# vim: ft=zsh
#
# A simple theme that displays relevant, contextual information.
#
# Authors:
#   Joseph Tannhuber <sepp.tannhuber@yahoo.com>
##################################################

# Load dependencies.
pmodload 'helper'

function prompt_budspencer_user {
unset _prompt_budspencer_user
if [[ -n "$SSH_CLIENT" || $UID -eq 0 ]]; then
    _prompt_budspencer_user="%(!.%{%K{${_prompt_budspencer_colors[4]}}%B%}.)%n%b"
fi
}

function pwdtoggle {
pwd_format=$PWDFORMAT[$(( $PWDFORMAT[(i)$pwd_format]%${#PWDFORMAT}+1 ))]
prompt_budspencer_pwd
zle reset-prompt
}
zle -N pwdtoggle
bindkey -M vicmd ' ' pwdtoggle
bindkey -M viins '^ ' pwdtoggle
bindkey -M emacs '^ ' pwdtoggle

function prompt_budspencer_pwd {
local pwd="${PWD/#$HOME/~}"
unset _prompt_budspencer_pwd
if [[ -n "$SSH_CLIENT" || $UID -eq 0 ]]; then
    _prompt_budspencer_pwd="%F{${_prompt_budspencer_colors[11]}}@%M"
    if [[ "$pwd_format" != "none" ]]; then
        _prompt_budspencer_pwd+=":"
    else
        _prompt_budspencer_pwd+=" "
    fi
fi
if [[ "$pwd_format" == "complete" ]]; then
    _prompt_budspencer_pwd+="%~ "
elif [[ "$pwd_format" == "truncated" ]]; then
    if [[ "$pwd" == (#m)[/~] ]]; then
        _prompt_budspencer_pwd+="$MATCH "
        unset MATCH
    else
        _prompt_budspencer_pwd+="${${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}//\%/%%}/${${pwd:t}//\%/%%} "
    fi
fi
}

function prompt_budspencer_indicator {
if [[ "%$editor_info[vimode]" == "%${_prompt_budspencer_colors[5]}" ]]; then
    print -n "%{${_prompt_budspencer_cursor[1]}%K{$editor_info[vimode]}%} REPLACE "
elif [[ "%$editor_info[vimode]" == "%${_prompt_budspencer_colors[8]}" ]]; then
    print -n "%{${_prompt_budspencer_cursor[2]}%K{$editor_info[vimode]}%} NORMAL "
else
    print -n "%{${_prompt_budspencer_cursor[3]}%K{$editor_info[vimode]}%} INSERT "
fi
}

function prompt_budspencer_symbols {
_prompt_budspencer_symbols="${VIM:+" %B%F{${_prompt_budspencer_colors[7]}}V%f%b"}"
_prompt_budspencer_symbols+="${RANGER_LEVEL:+" %B%F{${_prompt_budspencer_colors[7]}}R%f%b"}"
_prompt_budspencer_symbols+="%1(j. %F{${_prompt_budspencer_colors[9]}}⚙%f.)"
_prompt_budspencer_symbols+="%(?:: %B%F{${_prompt_budspencer_colors[5]}}↵%b)"
_prompt_budspencer_symbols+="%f%(!. %B%F{${_prompt_budspencer_colors[4]}}⚡%f%b.)"
}

function prompt_budspencer_precmd {
setopt LOCAL_OPTIONS
unsetopt XTRACE KSH_ARRAYS

# Format Username.
prompt_budspencer_user

# Format PWD.
prompt_budspencer_pwd

# Format Symbols.
prompt_budspencer_symbols

# Get Git repository information.
if (( $+functions[git-info] )); then
    git-info
fi

# Calculate runtime of previous command
cmdtime=$(( ($(date +%s) - $starttime)/60 ))
if [[ $cmdtime -eq 0 ]]; then
    runtime=""
else
    minutes=$(( $cmdtime%1440%60 ))"m"
    if [[ $cmdtime -ge 60 ]]; then
        hours=$(( $cmdtime%1440/60 ))"h"
        if [[ $cmdtime -ge 1440 ]]; then
            days=$(( $cmdtime/1440 ))"d"
        fi
    fi
    runtime="%F{${_prompt_budspencer_colors[1]}}%K{${_prompt_budspencer_colors[1]}}%F{${_prompt_budspencer_colors[11]}} $days$hours$minutes "
fi

# Removes the whitespace character at the end of the right prompt.
ZLE_RPROMPT_INDENT=0
}

function prompt_budspencer_preexec {
starttime=$(date +%s)
}

function prompt_budspencer_setup {
setopt LOCAL_OPTIONS
unsetopt XTRACE KSH_ARRAYS
prompt_opts=(cr percent subst)

# Load required functions.
autoload -Uz add-zsh-hook

# Add hook for calling git-info before each command.
add-zsh-hook precmd prompt_budspencer_precmd
add-zsh-hook preexec prompt_budspencer_preexec

# Load color palette.
_prompt_budspencer_colors=(241 254 136 166 160 125 61 33 37 64 0)

# Cursor colors.
unsupported_terminals=("fbterm" "st")
if [[ ! "${unsupported_terminals[(r)$TERM]}" == "$TERM" ]]; then
    _prompt_budspencer_cursor=(
    "\033]12;#dc322f\007"
    "\033]12;#268bd2\007"
    "\033]12;#b58900\007"
    )
fi

# Set editor-info parameters.
zstyle ':prezto:module:editor:info:completing' format "%B%F{${_prompt_budspencer_colors[5]}}...%f%b"
zstyle ':prezto:module:editor:info:keymap:primary:insert' format "${_prompt_budspencer_colors[3]}"
zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format "${_prompt_budspencer_colors[5]}"
zstyle ':prezto:module:editor:info:keymap:alternate' format "${_prompt_budspencer_colors[8]}"

# Set git-info parameters.
zstyle ':prezto:module:git:info' verbose 'yes'
zstyle ':prezto:module:git:info:action' format ":%%B%F{${_prompt_budspencer_colors[2]}}%s%f%%b"
zstyle ':prezto:module:git:info:added' format " %%B%F{${_prompt_budspencer_colors[10]}}✚%f%%b"
zstyle ':prezto:module:git:info:ahead' format " %%B%F{${_prompt_budspencer_colors[7]}}↑%f%%b"
zstyle ':prezto:module:git:info:behind' format " %%B%F{${_prompt_budspencer_colors[7]}}↓%f%%b"
zstyle ':prezto:module:git:info:branch' format '%F{${editor_info[vimode]}}%K{${_prompt_budspencer_colors[1]}}%F{${_prompt_budspencer_colors[11]}}  %b %s%F{${_prompt_budspencer_colors[1]}}'
zstyle ':prezto:module:git:info:commit' format '%F{${editor_info[vimode]}}%K{${_prompt_budspencer_colors[10]}}%F{${_prompt_budspencer_colors[11]}}  %.7c %s%F{${_prompt_budspencer_colors[10]}}'
zstyle ':prezto:module:git:info:deleted' format " %%B%F{${_prompt_budspencer_colors[5]}}✖%f%%b"
zstyle ':prezto:module:git:info:modified' format " %%B%F{${_prompt_budspencer_colors[8]}}✱%f%%b"
zstyle ':prezto:module:git:info:position' format '%F{${editor_info[vimode]}}%K{${_prompt_budspencer_colors[6]}}%F{${_prompt_budspencer_colors[11]}}  %p %s%F{${_prompt_budspencer_colors[6]}}'
zstyle ':prezto:module:git:info:renamed' format " %%B%F{${_prompt_budspencer_colors[6]}}→%f%%b"
zstyle ':prezto:module:git:info:stashed' format " %%B%F{${_prompt_budspencer_colors[9]}}✭%f%%b"
zstyle ':prezto:module:git:info:unmerged' format " %%B%F{${_prompt_budspencer_colors[7]}}═%f%%b"
zstyle ':prezto:module:git:info:untracked' format " %%B%F{${_prompt_budspencer_colors[2]}}●%f%%b"
zstyle ':prezto:module:git:info:keys' format \
    'prompt' '$(coalesce "%b" "%p" "%c")' \
    'rprompt' '%A%B%S%a%d%m%r%U%u'

# Define prompts.
PROMPT='%F{${_prompt_budspencer_colors[11]}}$(prompt_budspencer_indicator)%F{${editor_info[vimode]}}%k${git_info:+${(e)git_info[prompt]}}%k%f${_prompt_budspencer_symbols} '
RPROMPT='${git_info[rprompt]} ${runtime}%F{${editor_info[vimode]}}%F{${_prompt_budspencer_colors[11]}}%K{${editor_info[vimode]}} ${_prompt_budspencer_user}%K{${editor_info[vimode]}}${_prompt_budspencer_pwd}'
SPROMPT='zsh: correct %F{${_prompt_budspencer_colors[5]}}%R%f to %F{${_prompt_budspencer_colors[10]}}%r%f [nyae]? '
}

starttime=$(date +%s)
if [[ -z $PWDFORMAT ]]; then
    PWDFORMAT=("truncated" "complete" "none")
else
    PWDFORMAT=($=PWDFORMAT)
fi
pwd_format=$PWDFORMAT[1]
prompt_budspencer_setup "$@"
